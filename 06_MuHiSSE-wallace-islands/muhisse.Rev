################################################################################
#
# RevBayes Analysis: 
# MuHiSSE- East and West of Wallace's line X Islands v. continents 
# Modified by Rosana Zenil-Ferguson and Jenna McCullough
# 05/03/2021
################################################################################

#######################
# Reading in the Data #
#######################
# 0 = Island East
# 1 = Continent East
# 2 = Island West  
# 3 = Continent West

# how to code widespread species
## Species_A comes from Continent East and Island West (1 2)
## species_B is on both continents and islands AND both sides of line (0 1 2 3)
## species_C is widespread on islands/continents east of wallace's line (0 1)


setOption("useScaling","true")
NUM_STATES = 4
NUM_HIDDEN = 2
NUM_RATES = NUM_STATES * NUM_HIDDEN

###### This just defines a single model for all sites #######

### Read in sequence data for both genes

observed_phylogeny <- readTrees("corvides.720.no-outgroups.tre")[1]

data <- readCharacterDataDelimited("corvides-musse-wallace_X_line.txt",stateLabels=4,type="NaturalNumbers",delimiter="\t",headers=TRUE)

# we need to expand the data to the hidden states
data_exp <- data.expandCharacters( NUM_HIDDEN )

# Get some useful variables from the data. We need these later on.
taxa <- observed_phylogeny.taxa()

# set my move index
mvi = 0
mni = 0

H = 0.587405



####################
# Create the rates #
####################

### Specify a prior on the diversification and turnover rate

### Create the constant prior parameters of the diversification rates
rate_mean <- ln( ln(719/2.0) / observed_phylogeny.rootAge() )
rate_sd <- 2*H

for (i in 1:NUM_STATES) {
### Create a lognormal distributed variable for the diversification rate
speciation_alpha[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
moves[++mvi] = mvSlide(speciation_alpha[i],delta=0.20,tune=true,weight=3.0)


### Create a lognormal distributed variable for the turnover rate
extinction_alpha[i] ~ dnNormal(mean=rate_mean,sd=rate_sd)
moves[++mvi] = mvSlide(extinction_alpha[i],delta=0.20,tune=true,weight=3.0)


}


for (i in 1:NUM_HIDDEN) {

### Create an exponential distributed variable for the diversification rate
speciation_beta[i] ~ dnExp(1.0)
moves[++mvi] = mvScale(speciation_beta[i],lambda=0.20,tune=true,weight=2.0)

### Create an normal distributed variable for the turnover rate
extinction_beta[i] ~ dnNormal(0.0,1.0)
moves[++mvi] = mvSlide(extinction_beta[i],delta=0.20,tune=true,weight=2.0)

}

for (j in 1:NUM_HIDDEN) {
for (i in 1:NUM_STATES) {
if ( j == 1) {
speciation[i] := exp( speciation_alpha[i] )
extinction[i] := exp( extinction_alpha[i] )
} else {
index = i+(j*NUM_STATES)-NUM_STATES
speciation[index] := speciation[index-NUM_STATES] * exp( speciation_beta[j-1] )
extinction[index] := exp( extinction_alpha[i] + extinction_beta[j-1] )
}
}
}


#########################################################
# Set up the transition rate matrix for observed states #
#########################################################
## I defined very loosely my gamma priors for rates in the Q-mat
shape_pr := 0.5
#rate_pr := 1
############### Alternative definition or rate parameter
# Each transition rate between observed states are drawn
# from an exponential distribution with a mean of 10
# character state transitions over the tree.
 rate_pr := observed_phylogeny.treeLength()/10

# 0 = Island East
# 1 = Continent East
# 2 = Island West  
# 3 = Continent West


rate_01 ~ dnGamma(shape=shape_pr, rate=rate_pr) # East Island to Continent
rate_10 ~ dnGamma(shape=shape_pr, rate=rate_pr) # East Continent to Island

rate_03 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Island East to West
rate_30 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Island West to East

rate_02 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Island East to Continent West
rate_20 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Continent West to Island East

rate_12 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Continent East to West
rate_21 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Continent West to East

rate_13 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Continent East to Island West
rate_31 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Island West to Continent East

rate_23 ~ dnGamma(shape=shape_pr, rate=rate_pr) # Continent West to Island West
rate_32 ~ dnGamma(shape=shape_pr, rate=rate_pr) # West Island to Continent

moves[++mvi] = mvScale( rate_01, weight=2 )
moves[++mvi] = mvScale( rate_10, weight=2 )
moves[++mvi] = mvScale( rate_03, weight=2 )
moves[++mvi] = mvScale( rate_30, weight=2 )
moves[++mvi] = mvScale( rate_02, weight=2 )
moves[++mvi] = mvScale( rate_20, weight=2 )
moves[++mvi] = mvScale( rate_12, weight=2 )
moves[++mvi] = mvScale( rate_21, weight=2 )
moves[++mvi] = mvScale( rate_13, weight=2 )
moves[++mvi] = mvScale( rate_31, weight=2 )
moves[++mvi] = mvScale( rate_23, weight=2 )
moves[++mvi] = mvScale( rate_32, weight=2 )


for (i in 1:4) {
for (j in 1:4) {
q[i][j] := 0.0
}
}

q[1][2] := rate_01
q[2][1] := rate_10
q[1][4] := rate_03
q[4][1] := rate_30
q[1][3] := rate_02
q[3][1] := rate_20
q[2][3] := rate_12
q[3][2] := rate_21
q[2][4] := rate_13
q[4][2] := rate_31
q[3][4] := rate_23
q[4][3] := rate_32


#########################################################
# Set up the transition rate matrix for hidden states #
#########################################################

# We assume the transitions among the hidden states
# are all equal and drawn from an exponential distriubtion.
hidden_rate1 ~ dnExponential(rate_pr)
moves[++mvi] = mvScale(hidden_rate1,lambda=0.2,tune=true,weight=5)
hidden_rate2 ~ dnExponential(rate_pr)
moves[++mvi] = mvScale(hidden_rate2,lambda=0.2,tune=true,weight=5)

#### Here the hidden rates should go (alpha, beta, alpha, beta) for assymetrical models
R[1]:= hidden_rate1
R[2]:= hidden_rate2
R[3]:= hidden_rate1
R[4]:= hidden_rate2


######################################################################
# Create the rate matrix for the combined observed and hidden states #
######################################################################
rate_matrix := fnHiddenStateRateMatrix(q, R, rescaled=false)

#####################################
# Set up the root state frequencies #
#####################################

### Create a constant variable with the prior probabilities of each rate category at the root.
rate_category_prior ~ dnDirichlet( rep(1,NUM_RATES) )
moves[++mvi] = mvBetaSimplex(rate_category_prior,tune=true,weight=2)
moves[++mvi] = mvDirichletSimplex(rate_category_prior,tune=true,weight=2)


### Create the fix parameter for the age of the root set to the observed age
root <- observed_phylogeny.rootAge()


### rho is the probability of sampling species at the present
### I did not add any sampling of species but it is posible by setting this rho parameter

rho <- 719/832

#### construct a variable for the tree drawn from a birth death process
timetree ~ dnCDBDP( rootAge           = root,
speciationRates   = speciation,
extinctionRates   = extinction,
Q                 = rate_matrix,
pi                = rate_category_prior,
rho               = rho,
delta             = 1.0,
condition         = "time" )

### clamp the model with the "observed" tree
timetree.clamp( observed_phylogeny )
timetree.clampCharData( data_exp )


#############
# The Model #
#############


### workspace model wrapper ###
mymodel = model(rate_matrix)

### set up the monitors that will output parameter values to file and screen
monitors[++mni] = mnModel(filename="muhisse_wallace_island.log", printgen=1)
monitors[++mni] = mnScreen(printgen=1, rate_01, rate_23, speciation, extinction)


################
# The Analysis #
################

### workspace mcmc
mymcmc = mcmc(mymodel, monitors, moves, nruns=1, moveschedule="random")

### pre-burnin to tune the proposals
#mymcmc.burnin(generations=5000,tuningInterval=100)

### run the MCMC
mymcmc.run(generations=120000)

##############################
# Summarize ancestral states #
##############################

#observed_phylogeny <- readTrees("basicdata/fullmatchtree.nex")[1]
#anc_states = readAncestralStateTrace("/home/eeg/shared/twostatesse/hisse/output/anc_states_HiSSE_polynodipasymmetry250K.log")
#anc_tree = ancestralStateTree(tree=observed_phylogeny, ancestral_state_trace_vector=anc_states, include_start_states=false, file="/home/eeg/shared/twostatesse/hisse/output/anc_states_summaryHiSSEnodiptreeasymmetry250K.tree", burnin=5000, summary_statistic="MAP", site=0)

q()


